// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static Array twoToThe; // Cache nilai 2^0 sampai 2^15 biar cepet

    /** Initializes the library. */
    function void init() {
        var int i, val;
        let twoToThe = Array.new(16);
        let i = 0;
        let val = 1;
        while (i < 16) {
            let twoToThe[i] = val;
            let val = val + val; // val = val * 2 (tapi pake tambah)
            let i = i + 1;
        }
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) { return -x; }
        return x;
    }

    /** Returns true if the i-th bit of x is 1, false otherwise */
    function boolean bit(int x, int i) {
        return ~((x & twoToThe[i]) = 0);
    }

    /** Returns the product of x and y. 
     * Algorithm: Shift and Add (Standard Binary Multiplication)
     * Complexity: O(n) where n is number of bits (16)
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX, i;
        let sum = 0;
        let shiftedX = x;
        let i = 0;
        
        while (i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX; // Geser X ke kiri (<< 1)
            let i = i + 1;
        }
        return sum;
    }

    /** Returns the integer part of x/y. */
    function int divide(int x, int y) {
        var int q, absX, absY, res;
        
        if (y = 0) { do Sys.error(3); } // Division by zero
        
        let absX = Math.abs(x);
        let absY = Math.abs(y);

        if (absY > absX) { return 0; }
        
        // Optimasi: cek overflow y*2 < 0
        if ((absY + absY) < 0) { 
            let q = 0; 
        } else {
            let q = Math.divide(absX, absY + absY); // Rekursi: bagi dengan 2y
        }
        
        if ((absX - (2 * q * absY)) < absY) {
            let res = q + q;
        } else {
            let res = q + q + 1;
        }

        // Handle tanda negatif
        if ((x < 0) = (y < 0)) { return res; }
        else { return -res; }
    }

    /** Returns the integer part of the square root of x. 
     * Algorithm: Binary Search di level bit (dari bit 7 ke 0)
     * Complexity: O(n/2) = O(8) steps. Sangat cepat.
     */
    function int sqrt(int x) {
        var int y, j, temp, tempSq;
        
        if (x < 0) { do Sys.error(4); }
        
        let y = 0;
        let j = 7; // Mulai dari (16/2) - 1
        
        while (~(j < 0)) {
            let temp = y + twoToThe[j];
            let tempSq = temp * temp;
            
            // Cek: (temp^2 <= x) DAN (temp^2 > 0 buat handle overflow)
            if (~(tempSq > x) & (tempSq > 0)) {
                let y = temp;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) { return a; }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) { return a; }
        return b;
    }
}